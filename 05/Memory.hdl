// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:

	// 5432109876543210
	// 0000000000000000 -- memory
	// 0011111111111111 -- memory
	// 0100000000000000 -- screen
	// 0101111111111111 -- screen
	// 0110000000000000 -- kbd

	//  43
	// 000 == memory
	// 001 == memory
	// 010 == screen
	// 011 == kbd

	Not(in=address[14], out=doRam);
	And(a=doRam, b=load, out=doLoadRam);
	RAM16K(in=in, load=doLoadRam, address=address[0..13], out=ramOut);

	// muxable but I'm tired :)
	Not(in=address[13], out=notAddr13);
	And(a=address[14], b=notAddr13, out=doScreen);
	And(a=address[14], b=address[13], out=doKbd);

	And(a=load, b=doScreen, out=doLoadScreen);

	Screen(in=in, load=doLoadScreen, address=address[0..12], out=screenOut);
	Keyboard(out=kbdOut);

	Mux4Way16(sel=address[13..14], a=ramOut, b=ramOut, c=screenOut, d=kbdOut, out=out);
}
