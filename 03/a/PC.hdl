// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
	// If we increment
	Inc16(in=regOut, out=incOut);
	Mux16(a=regOut, b=incOut, sel=inc, out=incOrOut);

	// If we load
	Mux16(a=incOrOut, b=in, sel=load, out=loadOrOut);

	// If we reset
	Mux16(a=loadOrOut, b=false, sel=reset, out=resetOrOut);


	// load or stay
	Or(a=reset, b=load, out=intermediateDoLoad);
	Or(a=intermediateDoLoad, b=inc, out=doLoad);

	// State
	Register(in=resetOrOut, load=doLoad, out=out, out=regOut);
}
